;
; Copyright (c) 2020 Raspberry Pi (Trading) Ltd.
;
; SPDX-License-Identifier: BSD-3-Clause
;

; Side-set pin 0 is used for LOAD output
; Side-set pin 1 is used for SCLK output

.program SN74HC595
.side_set 2

.define N  3 ; Wait loop iteration count
.define P  0 ; Period adjustement

 .wrap_target
    set     x, 0      side 0 [P]
    pull noblock      side 0
    set     y, 31     side 0
shiftloop:
    out     pins, 1   side 0
    jmp y-- shiftloop side 2 ; Loop until Y hits 0
    set     x, N      side 1
waitloop:
    jmp x-- waitloop  side 0 [7] ; Loop until X hits 0
 .wrap                 ; Implicit jump to the first instruction

; Duration of a period is :
;   (P + 1) + 1 + 1 + 32 * 2 + 1 + (N + 1) * 8 = 76 + P + 8 * N
; We want a 20 Âµs period, with a 5 MHz clock --> 76 + P + 8 * N = 20 * 5 = 100
; so N = 3 and P = 0
